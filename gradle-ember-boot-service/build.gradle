apply plugin: 'org.springframework.boot'

clean {
    delete "src/main/resources/templates/index.html"
    delete "src/main/resources/static/assets"
}

configurations {
    js
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    js project(path: ":gradle-ember-ui", configuration: "js")
    compile("net.sourceforge.nekohtml:nekohtml:1.9.22")
}

sourceCompatibility = 1.8

task unpackJsProjects(type: Copy, dependsOn: configurations.js.buildDependencies) {
    def zips = configurations.js.collect {
        zipTree it
    }

    from zips, {
        include "index.html"
        into "templates"
    }

    from zips, {
        include "assets/**"
        into "static"
    }

    destinationDir new File("$project.projectDir/src/main/resources")
}

processResources.dependsOn "unpackJsProjects"
